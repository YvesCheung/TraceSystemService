plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
}

android.libraryVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    task "${taskName}" {
        doLast { delete "${buildDir}/intermediates/javac/${variant.name}/classes/android" }
    }
    variant.processJavaResources.dependsOn(taskName)
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        release(MavenPublication) {
            artifact "$buildDir/outputs/aar/${project.getName()}-release.aar"
            artifact sourcesJar
            groupId = group
            artifactId = rootProject.name
            version = version
            pom {
                developers {
                    developer {
                        name = 'YvesCheung'
                        email = '975135274@qq.com'
                    }
                }
                scm {
                    connection = 'https://github.com/YvesCheung/RollingText.git'
                    developerConnection = 'https://github.com/YvesCheung/RollingText.git'
                    url = 'https://github.com/YvesCheung/RollingText'
                }
            }
        }
    }
}